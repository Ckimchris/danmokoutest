pattern { bgm {
	1  simp.ex.stage
	9	_
} }
phase 0
	paction 0
		shift-phase-to 4
		

<!> dialogue #{
phase 0
	saction 0
		sync keine <8;4:> gsr { root zero } summonsup null null
		@ keine
			saction 0 :5
				move-target 2 io-sine cy 1
				dialogue simp.dialogue.ex.0
				sync _ <> 1upitem
				move-target 2 io-sine cxy -8 4
				cull
		shift-phase
#}

##2
<!> announce
phase 0
	saction 0
		stage-announce
		shift-phase


!{ power1()
sync powerup1 <> powerup2
	x-powerup-1 x-powerdown-1
	witha 0.7 lerp 0 1 t red pink
	witha 0.7 black
	1 1 0 0.5
!}

!!{ color3 { red pink black }

!{ fireu1()
async fireball-red/b <> gcr3 /i dl 70 6.2s <> {
	delay(lerpd(60, 0))
	sfx x-bubble
} target Lplayer s tprot px lerpt 0 0.4 4.4 3.6
!}
!{ obju1(y 2)
color tfairy summonr null(cy(5))
	movewrap~ 3 1.5 goy(%y) 5 gootherside(%y + 0.5)
		$fireu1
	{ hp 150 }
!}
!{ obju1_x(lr)
color tfairy summonr null(px(-4.6 * %lr))
	movewrap~ 5 1 gox(-2.7 * %lr) 2 goside(y)
		$fireu1
	{ hp 150 }
!}

!{ fires1()
async star-black/w <> gir2 lerpd(420, 280) (3 + d3d1) <> {
} gcr2 20 (7 + d3d2) <> {
	target a Lplayer
	sfx x-fire-star-2
} gsr2 5 <> {
	spread <70>
	center
} simple tprot px 2.8 { dir starrotb2 }
!}
!{ objs1(y 2.5)
color tlfairy summonr null(cy(5))
	movewrap 1 goy(%y) 3 goside(%y - 0.5)
		$fires1
	{ hp 650 }
!}

!{ firem1()
paction 0 :2
	async scircle-pink/b <> gcr3 (240 / dl) _ <> {
		sfx x-fire-burst-3
		while onscreen loc
	} gsr2c 3 {
		aimed
	} gsr2 2 <-0.2;:> {
		p this
	} s tprot px(* lerpt 0.3 0.6 2 1 - 1.8 * 0.4 p)
	async ellipse-* <> gir2 0 2 <> {
		color { pink/w red/w }
		preloop lr =f pm1 t
	} gcr3 /i dl 12 _ (* (/ &lr dl) <15h>) {
		while onscreen loc
		sfx x-fire-loop-1
	} gsr2c 4 {
	} s polar(
		lssht -2 0.6 (3 * t)
			lssht 2 1.4 (-1 * t) (3.2 * t),
		&lr * lssht -0.2 1 (36 * ^ t 2) (-8 * t))
!}
!{ objm1(y 2.3)
color tlfairy summonr null(cy(5))
	movewrap~ 3.5 1 goy(%y) 2 goside(%y - 0.5)
		$firem1
	{ hp 1200 }
!}


!{ firem2()
paction 0 :2
	async gdlaser-red/ <> gcr3 (160 / dl ^ 0.4) 9s <> { 
		sfx x-fire-burst-1
		while onscreen loc
		delay 0.6s
	} {
		gsr {
			offset(<-6;:>)
			target a Lplayer 
		} laser null 1 0.2 { dsfx }
		color lellipse-** gsr2 2 <1.4;:> {
			color { red pink }
			center
			bindLR
		} gsr2 (if(> dc 1, 3, 2)) <> {
			bank0(<>)
			sltarget ra Lplayer
			p this
		} gsr2 5 <> {
			spread * &rl <90>
			center
			colorf({ /w / /b }, abs(t - &times // 2))
		} s tprot px((1.3 +  0.3 * p) * lerpt 0.1 0.5 3 1.6)
	}
!}
!{ objm2(lr)
color tlfairy summonr crossdown(%lr, 1.4 * t)
	$firem2
	{ hp 850 }
!}

!{ fireh1()
paction 0 :3
	async fireball-*/ <1;:> gcr3 (5 / dl) 11s <15h> {
		sfx x-fire-loop-1
		p this
	} gsr2c 3 {
		color $color3
		bank0 a * 15.1 p
	} s tprot px lerpt 0 0.6 1 3
	$power1
	_ 1 async gdlaser-*/b <-90> gcr2 10 12 <> {
			color $color3
			p this
		} gsr2 2 <> { 
			p add
		} laser null 0.5 (- 8.8 * 8f p1) {
			dsfx
			rotate	0 * pm1 p2 lerpsmooth esine010 0 (- 9.5 * 17f p1) t (+ -170 * -4 p1) -9
		}
!}
!{ objh1(y 2.5)
color tlfairy summonr null(cy(5))
	phase 0
		finish
			behpool-control gdlaser-* softcullall cwheel
		saction 0
			imovewrap 1.3 goy(%y) 3 goside(%y - 0.5)
				$fireh1
			move _ tpnrot cy 7 ##I don't have a better way of destroying without triggering finish than autoculling
	{ hp 4000 }
!}

!{ l(x -2.7)
<%x%;:>
!}
!{ r(x 2.7)
<%x%;:>
!}
!!{ lr 2.7
!!{ w 5.4


<!> stage
phasej 71 0 #{
	paction 0
		async _ <0.5;:> gcr2 30 6 <-0.75;:> {
			center
			p this
		} $obju1(2.4 - 0.2 * p)
		_ 1 async _ $l $objs1(1.2)
	paction 1
		async _ <> gcr2 0 2 <> {
			bindLR
		} $objm2(&lr)
	paction 5
		async _ <-0.6;:> gcr2 30 6 <0.75;:> {
			center 
			p this
		} $obju1(1.4 + 0.6 * softmod(2, p))
		_ 1 async _ <2;:> $objm1
	paction 3
		async _ <-2.1;:> $objm1(2)
		async _ $l $objs1(0.5)
		async _ <0.6;:> gcr2 30 6 <-0.75;:> {
			center 
			p this
		} $obju1(1.4 + 0.3 * p)
	paction 4.4
		async _ <> $objm1
		_ 0.5 async _ <> gsr2 2 rx($w) {
			center
		} $objs1(1)
	paction 5
		dangerleft
		_ 1 async _ <2;:> $objm1(1.8)
		_ 4 async _ <2;:> $objm1(2.2)
		_ 2 async _ <;1:> gcr2 6 8 ry(-0.6) {
			center
		} $obju1_x(1)
	paction 3
		async _ <> $objh1
	paction 5
		async _ <-0.4;:> gcr2 30 4 <-0.5;:> {
			p this
		} $obju1(2.5 - 0.5 * p)
		_ 1 async _ $l $objs1(0.5)
		_ 3 async _ $r $objs1(0.5)
		_ 4 async _ <1.8;:> $objh1
		_ 7 async _ <;1:> gcr2 6 9 ry(-0.6) {
			center
		} $obju1_x(1)
	paction 4
		_ 1 async _ <> $objs1(2)
		_ 3 async _ $r $objs1(1)
		_ 5 async _ <> $objm2(-1)
		_ 5 async _ $l $objs1(0.5)
	paction 4
		async _ <-2;:> $objh1
		_ 2.8 async _ $r $objs1(1.5)
		_ 2.5 async _ <;1:> gcr2 6 8 ry(-0.6) {
			center
		} $obju1_x(-1)
#}

##4. Mid-boss
<!> midboss
phase 0
	saction 0
		boss simp.mokou
		shift-phase

<!> stage #{
phasej 31 0
	paction 0
		async _ <2;:> $objh1
		_ 1.5 async _ $l $objs1(1)
		_ 1 async _ <-1;:> $objs1(1.5)
	paction 5
		async _ <> $objm2(-1)
		_ 0.5 async _ <0.2;:> gcr2 30 6 <-0.6;:> {
			p this
			center
		} $obju1(1.2 + 0.3 * p)
	paction 3
		async _ <-2;:> $objh1
		_ 1.5 async _ <;1:> gcr2 6 8 ry(-0.6) {
			center
		} $obju1_x(-1)
	paction 7
		async _ <> $objh1
		_ 2 async _ <;1:> gcr2 6 5 ry(-0.6) {
			center
		} $obju1_x(-1)
		_ 2 async _ <;1:> gcr2 6 5 ry(-0.6) {
			center
		} $obju1_x(1)
		
		

#}

<!> stage #{
phase 26
	paction 0
		dangerbot
		dangerleft2
		timer restart pt
!!{ firef /i ^ dl 0.5 lerp 0 14 timersec(pt) 3.5sh 2.7sh
		async crow <-3;-5:180> gcr2 $firef _ <180> {
			p this
		} summon tprot cy 1
			paction 0 :2
				async gdlaser-*/b <-2;:> iparent this gir {
					delay * 2s rangemodh p
					colorf { yellow purple } p
				} laser null 1.5 1 { repeat sfx2 `` x-laser-on }
				wait _
			{ hp 3000 }
		async crow <-6;-3.8:180> gcr2 $firef _ <180> {
			p this
		} summon tprot cx 1
			paction 0 :2
				async gdlaser-*/b <-2;:90> iparent this gir {
					delay * 2s rangemodh p
					colorf { yellow purple } p
				} laser null 1.5 1 { repeat sfx2 `` x-laser-on }
				wait _
			{ hp 3000 }

#}


##6
<!> dialogue #{
phase 0
	saction 2
		dialogue simp.dialogue.ex.3
		shift-phase
#}

##7. Mid-boss
<!> midboss
phase 0
	saction 0
		boss simp.yukari2
		shift-phase


## 8. End-boss
<!> endboss
phase 0
	saction 2
		boss simp.shinki
		shift-phase


##9
<!> announce
phase 0
	paction 0
		stage-deannounce
		lifetoscore 1500000
	paction 1
		shift-phase




