# Tutorial 5: Difficulty Controls

By default, the engine has the following difficulty levels: Easy, Normal, Hard, and Lunatic.

You can add or remove levels by modifying the <xref:Danmokou.Core.FixedDifficulty> enum in Danmaku/Core/Enums.cs, and also adjusting the values in the enum helpers in that file. 

Start with this code:

```python
<#> warnprefix
pattern { }
phase 0
	paction(0)
		shift-phase-to 1
		

<!> type non  `Difficulty` #{
<!> root 0 1
phase 0
	paction(0)
!!{ crpt 30
		async arrow-red/w <> gcrepeat {
			wait(120)
			times(_)
		} gsrepeat {
			times($crpt)
			circle
		} s tprot cx 2
```

This should all be familiar. 

In the earlier tutorials, I stressed using the R key to instantaneously reload scripts. There are in fact other script reload keys, and you may have pressed a few of them by accident. They are defined in Danmaku/Core/GameManagement.cs:TryTriggerLocalRestart as follows: 

- T: Reload on Easy
- Y: Reload on Normal
- U: Reload on Hard
- I: (i) Reload on Lunatic

Try pressing some of these keys while the script is running. You should see the difficulty marker in the top right of the game UI change, although the pattern will stay the same.

Now, let's make some difficulty variation. Change the macro line to `!!{ crpt (dl * 30)`.

Reload the script in several difficulties. You'll see that the number of bullets changes every time. What's this magic number `dl`?

Internally, each difficulty has an associated *counter* and *value*. The counter is 0 on Easy, 1 on Normal, 2 on Hard, and 3 on Lunatic. The value is a multiplier which is roughly equal to `sqrt(2)^(counter-1)`, ie. each difficulty is approximately 40% greater than the previous difficulty. You don't need to remember this, and if you think these numbers are bad, you can change them in Danmaku/Core/Enums.cs.

The function `dl` returns the ratio `CurrentDifficultyValue / LunaticDifficultyValue`. Effectively, we are using 30 as the reference value on Lunatic difficulty, and scaling it multiplicatively for all other difficulties.

If you have multiple layers of bullets, then using `dl` on everything will probably make the layers unbalanced amongst each other on other difficulties. To resolve this, you can tweak the difficulty variable by raising it to a power. For example, `!!{ crpt (^(dl, 0.4) * 30)` makes the difficulty vary less than `!!{ crpt (dl * 30)`. You can use a different adjustment for each layer.

We could also scale the waiting time using `dl`, although we should divide it, since higher difficulties have lower waiting times:

```python
		async arrow-red/w <> gcrepeat {
			wait(120 / dl)
			times(_)
		} gsrepeat {
			times(30)
			circle
		} s tprot cx 2
```

In addition to `dl`, there are also `dn`, and `dh` (guess what they do?), and you can add any that you need to <xref:Danmokou.DMath.Functions.ExMDifficulty>.

However, sometimes we want an additive modifier instead of a multiplicative one. In this case, we should use dc:

```python
		sync gdlaser-blue/b <> gsrepeat {
			times(6 + dc)
			circle
		} laser null 1 2 {
			dsfx
		}
```

This will spawn 6 lasers on Easy and 1 more for each difficulty above that.

There are also some convenience methods for grouping difficulties. For example, d3d2 returns -2 for Easy, 0 for Normal/Hard, and 2 for Lunatic. You can always add new convenience functions to <xref:Danmokou.DMath.Functions.ExMDifficulty>.

```python
		async triangle-green/w <> gcrepeat {
			wait(8)
			times(10)
			target a Lplayer
		} gsrepeat {
			times(5 + d3d2)
			spread <80>
			center
		} s tprot cx 2
```

The last method for difficulty controls is using `select`, which is effectively an array indexing operator. I don't recommend this as it's kind of verbose and inelegant, and it requires manually setting values for all difficulties.

```python
!!{ crpt select dc { 15 20 25 30 }
		async arrow-red/w <> gcrepeat {
			wait(120)
			times(_)
		} gsrepeat {
			times($crpt)
			circle
		} s tprot cx 2
```

