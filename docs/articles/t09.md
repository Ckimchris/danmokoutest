# Tutorial 9: Repeater Modifiers

In this tutorial, we're going to go through almost all of the repeater modifiers (<xref:Danmokou.Danmaku.Options.GenCtxProperty>) in the engine. Use this page as a reference!

## Bank/Bank0

Let's say we want to summon bullets in this kind of structure: 

![Unity_V9myf0XFax](../images/Unity_iQdM0y9OQ8.jpg)

What exactly are we trying to do here? Well, we want to go around six points in a circle, and for each one summon this weird spirally shape. How might you do this?

Your first thought might be to use the `circle` or `rv2incr` prop to summon each of the six colored groups, and then use a nested repeater to summon all of the bullets for each group. However, there's a bit of an issue here. If you use `rv2incr` with an angle in the nested repeater, it will shift the summon around the boss, instead of around the origin of each group.

The `bank` command allows us to deal with this issue. When used on entering a repeater, it shifts the current rotational X and Y coordinates into nonrotational coordinates, and also adds a new offset. In essence, it allows us to perform "inner repeats". 

`bank0` is similar, but also resets the rotational angle on entry.

```python
		sync(ellipse-*/w, <2;:>, gsrepeat {
			times(6)
			circle
			color({ red pink purple blue teal green })
		} gsrepeat {
			bank <1;:>
			times(60)
			rv2incr <-0.02;0:6>
		} s(null))
```

## Bind*

See [the tutorial on empty-guiding](t08.md) for `bindArrow`.

`bindItr(myLoopName)` assigns the current loop iteration (zero-indexed) to the bound variable `&myLoopName`.

`bindLR` assigns 1 to the bound variable `&lr` and -1 to `&rl` if the loop iteration is even-numbered, and -1 to `&lr` and 1 to `&rl` if the loop iteration is odd-numbered.

`bindUD` is the same, but with `&ud` and `&du`.

## Cancel/Clip/While/Unpause

These modifiers allow controlling the execution flow of the repeater loop.

`Cancel` takes a boolean function and checks it before every iteration. If it is true, the repeater stops.

```python
		sync(ellipse-red/w, <2;:>, gsrepeat {
			times(60)
			circle
			bindItr(i1)
			cancel(> &i1 10)
		} s(null))
```

`Clip` is the same, but is checked once at the beginning of the repeater only.

```python
		sync(ellipse-red/w, <2;:>, gsrepeat {
			times(60)
			circle
			bindItr(i1)
		} gsrepeat {
			clip(< &i1 20)
		} s(null))
```

`While` and `Unpause` are time-based and cannot be used in GSRepeat.

`While` takes a condition and only steps through the repeater while the condition is true. If the condition is not true, it pauses indefinitely. Listen to the sound effects in this example.

```python
		move(_, nroffset(px(sine(5, 3, t))))
		async(ellipse-red/w, <1;:>, gcrepeat {
			times(_)
			wait(4)
			rv2incr(<4>)
			bindItr(i1)
			while(> x -2)
			sfx x-fire-burst-2
		} s(rvelocity(lerpt(2, 3, zero, cx(2)))))
```

`Unpause` runs a state machine whenever a `While` condition is unpaused after being paused. Add this modifier to the above code:

```python
			unpause(async(scircle-purple/b, <>, gsrepeat { 
				times(10)
				circle
			} s(rvelocity(cx(4)))))
```

- What do you think would happen if you tried using `while` or `unpause` in a GSRepeat? What about if you tried using `unpause` without `while`?

## Circle/Spread/RV2Incr

`rv2incr` is the most basic way of adjusting bullet offsets between firing. After each iteration, it adds the provided RV2 to `&rv2`, which is the current bullet offset.

```python
		async(ellipse-red/w, <1;:>, gcrepeat {
			wait(4)
			times(30)
			rv2incr(<7>)
		} s(null))
```

`spread` allows you to instead specify a range over which you want bullets to be summoned. Note that when using `spread(X)`, the first bullet will summon at offset 0 and the last bullet will summon at offset X. This means that `spread(<360>)` is *not* the same as `circle`, as `spread(<360>)` would make the first and last bullets overlap.

```python
		async(ellipse-red/w, <1;:>, gcrepeat {
			wait(4)
			times(30)
			spread(<180>)
		} s(null))
```

`circle` is similar to `spread`; it simply summons all the bullets evenly in a circle.

```python
		async(ellipse-red/w, <1;:>, gcrepeat {
			wait(4)
			times(30)
			circle
		} s(null))
```

Note: you can use `spread` and `circle` with non-fixed repeater counts. Try using `	times(rand(10, 50))`. Internally, they use the `&times` bound variable. You could write: `rv2incr(angle(360 / &times))` instead of `circle`.

## Color/Colorf/ColorR

These are all modifiers that allow merging colors via wildcard rules. 

`Color` is the most basic form.

```python
		async(ellipse-*/w, <1;:>, gcrepeat {
			times(40)
			wait(3)
			circle
			color({ red blue green })
		} s(null))
```

`ColorR` merges the colors in the reverse direction, so you can merge even if the original style doesn't have a wildcard. Normally, that would cause an override.

```python
		async(ellipse, <1;:>, gcrepeat {
			times(40)
			wait(3)
			circle
			colorr({ *-red/ *-blue/ *-green/ })
		} s(null))
```

- What happens if you try this with `color` instead?

`Colorf` is like `color`, but instead of looping through the array, it allows you to provide an indexing function that selects one of the colors.

```python
		async(ellipse-*/w, <1;:>, gcrepeat {
			times(40)
			wait(3)
			circle
			colorf({ red blue green }, t / 5)
		} s(null))
```

## Delay/Wait/Wait-Child

These are not allowed on GSRepeat.

`Delay` creates a delay in frames before the first invocation of the repeater.

`Wait` creates a delay in frames between successive invocations of the repeater. 

Recall that you can also use `wait-child` for GIRepeat and GTRepeat to wait for the child to finish executing before continuing.

```python
		async(ellipse-pink/w, <1;:>, gcrepeat {
			delay(120)
			times(40)
			wait(3)
			circle
		} s(null))
```

## Root/RootAdjust

By default, bullets are summoned relative to the entity executing the state machine in question.

`Root` overrides this to be a fixed position.

`RootAdjust` overrides this to be a fixed position, but also adds to `&rv2` so the final summoning location is unchanged.

Try running the following code with `root`, `rootadjust`, and then neither.

```python
		position 0 2
		move _ nroffset(px(sine(4, 3, t)))
		async(ellipse-pink/w, <0.5;:>, gcrepeat {
			times(_)
			wait(73)
		} gsrepeat {
			root(cxy(-2, 0))
			times(20)
			circle
		} s(rvelocity(px(lerpt(1, 2, 0, 2)))))
```

## Start/Preloop/Postloop/End

These are commands that perform mathematical operations on bound data during specified times in the repeater.

`Start` occurs when the repeater starts,

`Preloop` occurs before each iteration,

`Postloop` occurs after each iteration,

and `End` occurs when the repeater ends. There is currently not much use for `End`.

All of these functions take an array of GCRule as an argment. GCRule is basically an assignment operation with four elements:

`REFERENCE OPERATOR TYPE VALUE`

A reference may either be a bound variable name, like `myVar` (which would be created on assigment) or `rv2` (which exists, and can be edited), or a member of a bound variable name, like `rv2.rx` (the rotational-x component of the V2RV2 `&rv2`).

An operator may be one of `=`, `+=`, `*=`, `-=`, `/=`, `//=` (`//` is floor division). You can `=` to a reference that does not yet exist, but the other operators require that the reference exists.

The type declares the type of the VALUE on the right hand side. It may be one of `f` (float), `v2` (vector2), `v3` (vector3), or `rv` (V2RV2). 

The value is simply a function that returns the declared type.  

Try figuring out why the first bullet summons in a different place in this example.

```python
		sync(ellipse-pink/w, <1;:>, gsrepeat {
			times(60)
			circle
			start {
				myVar =v2 cxy(2, 0)
			}
			preloop {
				myVar.x +=f 0.04
			}
			postloop {
				rv2.rx =f [&myVar].x
			}
		} s(null))
```

## Expose

See the [Danmokou Chimera](t03.md) tutorial for an explanation.

## Face

When summoning bullets, a global rotation is applied to the offset and the bullet summon. By default, this is the original summoning angle of the executor. For bosses, this value is zero, but for subsummons, the value will often not be zero.

Try running the following code and changing the `face` argument. `original` is the default value. Try `velocity`, which rotates bullets by the current movement direction, and `derot`, which derotates bullets. (There is also `rotvel`, but it's not really useful.)

```python
		sync(icrow, <1;:>, gsrepeat {
			times(4)
			circle
			bindItr(ic)
		} summon(
			rvelocity(lerpt(0.5, 1.5, cx(2), cy(2))),
			async(circle-*/w, <>, gcrepeat {
				face(original)
				colorf({ red blue yellow pink }, &ic)
				times(_)
				wait(12)
				while(onscreen(loc))
			} s(rvelocity(cx(-1)))),
			{ }
		))
```

## For/Times/MaxTimes

`times` sets the number of times that a repeater will execute. It may take a function as an argument. `maxtimes` indicates to the repeater what the maximum number of times might be. This is metadata that is used primarily for mod parametrization (see the tutorial on [empty-guiding](t08.md)), and you usually don't need to provide it unless you get an error message telling you to.

`for` sets the maximum number of frames that a repeater is allowed to run for. Between `times` and `for`, the repeater will stop when either one is not satisfied. `for` cannot be used with GSRepeat, which always executes in 0 frames.

This code may finish drawing all the lasers before the 2 seconds are up, or it may not. Run it a few times and see what happens.

```python
		async(gdlaser-teal/, <>, gcrepeat {
			for(2s)
			times(rand(20, 40))
			wait(8)
			circle
		} laser null 1 1 {})
```

## FRV2

`FRV2` is one of the most powerful modifiers. It is similar in concept to `rv2incr`, but instead of simply providing an increment, you provide a function that tells the repeater what the offset should be for each iteration.

This is the standard method of implementing BoWaP in DMK.

```python
		async(fireball-*/, <>, gcrepeat {
			times(_)
			wait(8)
			frv2(angle(sine(142, 1200, t)))
		} gsrepeat {
			times(5)
			circle
			color { purple pink red orange yellow }
		} s(rvelocity(cx(4))))
```

Here's another version of the `bank` example with a polar equation.

```python
		sync(ellipse-*/w, <3;:>, gsrepeat {
			times(6)
			circle
			color({ red pink purple blue teal green })
		} gsrepeat {
			bank <>
			times(60)
			frv2(rot(1 + cosine(1 / 3, 0.5, t / &times), 0, 360 * t / &times))
		} s(null))
```

## NoOp

This modifier does nothing.

## OnLaser

See the [laser tutorial](t04.md) for details.

## Parametrization

See the [empty-guiding tutorial](t08.md) for details.

## SAOffset

This is an advanced modifier which allows you to summon bullets along arbitrary equations. It is similar to FRV2, but it is far more generalized and requires far more complex input. Reference `Patterns/examples/summonalong` for examples of how to use this in more complex situations.

The third argument to SAOffset is an offset equation to decide the location of the i'th bullet. This will be converted into rotational coordinates. The second argument is an angle offset applied to all the bullets. 

The main unique feature of SAOffset is the first parameter, `SAAngle`, which is an enum that decides the angle handling for summoned bullets.

- o (original): The angle offset is added to all bullets directly. This means it will effectively rotate the entire summoned bullet body. 
- bo (bank-original): The position is banked and then the angle offset is added.
- br (bank-relative): The position is banked, then the angle is set to the angle from the root, and then the angle offset is added.
- bt (bank-tangent): The position is banked, then the angle is set to the tangent angle of the function, and the angle offset is added.

Here's an example to get you started. Try replacing "bo" with the above values.

```python
		async(triangle-*/w, <>, girepeat {
			times(4)
			circle
			color { black red blue yellow }
		} gcrepeat {
			times(180)
			wait(3)
			saoffset(bo, 30, pxy(0.07 * t, sine(40, 1.3, t)))
		} s(rvelocity(lerpt(1, 2, zero, cx(2)))))
```

## SaveF/SaveV2

These will be discussed in a later tutorial on bullet controls.

## Sequential/Alternate

By default, a generalized repeater will run all of its children at the same time. However, there are times where you want to run only one of the children, or when you want to run the children sequentially instead.

`Alternate(X)` makes the repeater run only child #X each iteration. The standard way to use this is `Alternate(t)`, which loops through the children one at a time.

`Sequential` makes the repeater run the children in sequence. Note that this only has an effect when used in GIRepeat or GTRepeat. 

```python
		async(_, <>, girepeat {
			times(4)
			wait(120)
			circle
			color { purple red blue yellow }
			##alternate t
			sequential
		} {
			gcrepeat {
				colorr gdlaser-*/b
				wait(20)
				times(3)
				rv2incr(<10>)
			} laser(null, 1, 1, { dsfx })
			gcrepeat {
				colorr gdlaser-*/w
				wait(20)
				times(3)
				rv2incr(<-10>)
			} laser(null, 1, 1, { dsfx })
		})
```

## SFX/SFXf/SFXfIf/SFXIf

`SFX` and all its siblings create sound effects when used. They take an array of sound effects and loop through them.

By using the `f` types, you can provide an indexer for the specific sound effect you want.

By using the `If` types, you can provide a predicate that allows not playing a sound effect in certain conditions.

## Target/SLTarget

These two modifiers are responsible for aiming bullets. They run once at the beginning of the repeater.

They take two arguments: a control method, and a target (which is usually `Lplayer`, the location of the player). The control method is one of the following:

- NX: Add `(target - source).x` to `rv2.nx`
- NY: Add `(target - source).x` to `rv2.ny`
- RX: Add `(target - source).x` to `rv2.rx`
- RY: Add `(target - source).x` to `rv2.ry`
- RA: Add the angle from source to target to `rv2.a`
- A: Rotate `rv2` by the angle from source to target (including NX/NY)

In `Target`, source is the position of the firing entity. In `SLTarget`, source is the real position of the offset `rv2`. 

You will almost always use either `RA` or `A` as control methods. The others are useful almost exclusively for creating laser grids. 

Here is an example you can play with. Make sure to move around to see how the bullets track with time. Try the following:

- Use `target a`
- Use `sltarget ra`
- Move the command into `gcrepeat` instead (you'll probably want to move it back afterwards)
- Use `target rx` and `target ry`
- Use `sltarget rx` and `sltarget ry`

```python
		async(gem-*/, <>, girepeat {
			times(3)
			rv2incr(<-1;0.2:;:>)
			color { purple red blue }
		} gcrepeat {
			times(_)
			wait(12)
		} gsrepeat {
			target(ra, Lplayer)
		} s(rvelocity(lerpt(0, 1, zero, cx(4)))))
```

Note that SLTarget + a will give you strange results.

## Timer

This modifier restarts a timer before each iteration. 

```python
		sync(sun-red/, <>, s(nroffset(px(timersec(myTimer)))))
		async(_, <>, gcrepeat {
			timer(myTimer)
			wait(120)
			times(_)
		} noop)
```

## TimeReset

Bullets have a variable `&st` which is the "summoning time" of the bullet. This is the amount of time elapsed between the start of the topmost general repeater in its summon hierarchy and the actual summoning of the bullet. If you add this modifier, then the timer will be reset at the beginning of each iteration of the repeater.

```
		async(arrow-pink/w, <>, girepeat {
			wait(30)
			times(20)
			timereset
			rv2incr(<16>)
		} gcrepeat {
			wait(5)
			times(10)
		} s(roffset(px(1 + 3 * &st))))
```



